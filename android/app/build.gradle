plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
}

def props = new Properties()
props.load(new FileInputStream(file("secret.properties")))

android {
    compileSdkVersion "android-S"
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.tomcurran.cheetah"
        minSdk 30
        targetSdk 30
        versionCode 1
        versionName "0.1.0"

        buildConfigField "String", "STRAVA_OAUTH_CLIENT_ID", "\"${props.stravaOAuthClientId}\""
        buildConfigField "String", "STRAVA_OAUTH_CLIENT_SECRET", "\"${props.stravaOAuthClientSecret}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file(props.keyStore)
            storePassword props.keyStorePassword
            keyAlias props.keyAlias
            keyPassword props.keyAliasPassword
        }
    }

    productFlavors.all {
        ext.stravaOAuthRedirectScheme = null
        ext.stravaOAuthRedirectDomain = "auth"
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            stravaOAuthRedirectScheme = "cheetah-dev.tomcurran.org"
            buildConfigField "String", "FIREBASE_CUSTOM_TOKEN_HOST", "\"https://cheetah-dev.tomcurran.org\""
        }
        appstore {
            dimension "version"
            stravaOAuthRedirectScheme = "cheetah.tomcurran.org"
            buildConfigField "String", "FIREBASE_CUSTOM_TOKEN_HOST", "\"https://cheetah.tomcurran.org\""
        }
    }

    buildTypes {
        all {
            productFlavors {
                all {
                    manifestPlaceholders = [
                            'appAuthRedirectScheme': stravaOAuthRedirectScheme,
                    ]
                    buildConfigField "String", "STRAVA_OAUTH_REDIRECT_URI", "\"$stravaOAuthRedirectScheme://$stravaOAuthRedirectDomain\""
                }
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.activity:activity-compose:1.3.0-rc01'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07"
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha01'
    implementation 'net.openid:appauth:0.9.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation platform('com.google.firebase:firebase-bom:28.2.1')
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}